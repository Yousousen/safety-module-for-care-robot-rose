/* constants to denote the minimum and maximum values a sensor can measure */
const BEG_SENSOR = 0
const END_SENSOR = 3
/* constants to denote what values denote safe and unsafe behaviour*/
const BEG_SAFE = BEG_SENSOR
const END_SAFE = 1
const BEG_UNSAFE = END_SAFE + 1
const END_UNSAFE = END_SENSOR
/* constants for battery status */
const EMPTY_BATTERY = 0
const FULL_BATTERY  = 3
const MID_BATTERY   = FULL_BATTERY / 2
/* constants for grip strength */
const MIN_STRENGTH = 0
const MAX_STRENGTH = 3 
const MID_STRENGTH = MAX_STRENGTH / 2 
/* Constants for wifi strength monitor. */
const MIN_WIFI_STRENGTH = 0
const MAX_WIFI_STRENGTH = 4
const MID_WIFI_STRENGTH = MAX_WIFI_STRENGTH / 2

/* ranges for the respective constants */
range SAFE_RANGE          = BEG_SAFE..END_SAFE
range UNSAFE_RANGE        = BEG_UNSAFE..END_UNSAFE
range SENSOR_RANGE        = BEG_SENSOR..END_SENSOR
range STRENGTH_RANGE      = MIN_STRENGTH..MAX_STRENGTH
range LOW_STRENGTH_RANGE  = MIN_STRENGTH..MID_STRENGTH
range HIGH_STRENGTH_RANGE = MID_STRENGTH..MAX_STRENGTH
range CHARGE_RANGE        = EMPTY_BATTERY..FULL_BATTERY
range LOW_CHARGE_RANGE    = EMPTY_BATTERY..MID_BATTERY
range GOOD_CHARGE_RANGE   = MID_BATTERY..FULL_BATTERY
range WIFI_RANGE          = MIN_STRENGTH..MAX_STRENGTH
range LOW_WIFI_RANGE      = MIN_WIFI_STRENGTH..MID_WIFI_STRENGTH
range GOOD_WIFI_RANGE     = MID_WIFI_STRENGTH..MAX_WIFI_STRENGTH

/* Controls measured values */
Controller = (checkAcceleration->AccelerationMonitor
             |checkCamera->CameraMonitor
             |checkLaser->LaserMonitor
             |checkBattery->BatteryMonitor
             |checkArm->ArmMonitor
             |checkLift->BodyMonitor
             |checkJoint->JointMonitor
             |checkWifi->WifiMonitor
             ),

/* Monitor acceleration measured with safety module or Rose */
AccelerationMonitor = (safe_acc->safe_acceleration->Controller
                      |unsafe_acc->unsafe_acceleration->signal->Controller),

/* Monitor safety module's or Rose's camera */
CameraMonitor = (safe_detect->safe_detection->Controller
                |unsafe_detect->unsafe_detection->signal->Controller),

/* Monitor Rose her LIDAR */
LaserMonitor = (safe_scn->safe_scan->Controller
               |unsafe_scn->unsafe_scan->signal->Controller),

/* Monitor the grip arm of Rose */
ArmMonitor = (low_str->low_strength->Controller
              |high_str->high_strength->signal->Controller),

/* Monitor the battery charge of Rose */
BatteryMonitor = (good_bat->good_battery->Controller
                 |low_bat->low_battery->signal->Controller),

/* Monitor the lift of Rose her body */
BodyMonitor = (getLift->Controller),

/* Monitor the state of the joints */
JointMonitor = (getJointState->Controller),

/* Monitor the strength of the wifi signal */
WifiMonitor = (good_wf->good_wifi->Controller
              |low_wf->low_wifi->signal->Controller).


/* Operator gets signalled by safety controller */
Operator = (readError->Operator).

/* Sensors and peripherals */
Accelerometer = (measure[i:SENSOR_RANGE]->Accelerometer).
Camera        = (measure[i:SENSOR_RANGE]->Camera).
LaserScanner  = (measure[i:SENSOR_RANGE]->LaserScanner).
Arm           = (getGripStrengh[i:STRENGTH_RANGE]->Arm).
Battery       = (getStatus[i:CHARGE_RANGE]->Battery).
Body          = (getLift[i:SENSOR_RANGE]->Body).  /* Of interest for us is the lift of the body. */
Joint         = (getJointState[i:SENSOR_RANGE]->Joint). 
WifiInterface = (getWifiStrength[i:WIFI_RANGE]->WifiInterface).


/* Actuator drivers */
MotorDriver = (speedUp->MotorDriver
              |slowDown->MotorDriver
              |turnRight->MotorDriver
              |turnLeft->MotorDriver
              ).
CameraDriver = (turnLeft90degrees->CameraDriver
               |turnRight90degrees->CameraDriver
               ).
ArmDriver = (increaseGripStrength->ArmDriver
            |decreaseGripStrength->ArmDriver).
BodyDriver = (increaseLift->BodyDriver
             |decreaseLift->BodyDriver).
/* TODO: What can we do with the joints? */

/* The system consists of the controller, the operator and all the sensors,
 * peripherals and actuators. */
||System = (controller:Controller
           || operator:Operator
           || {smAccelerometer,roseAccelerometer}:Accelerometer
           || {smCamera,roseCamera}:Camera
           || roseLidar:LaserScanner
           || roseBattery:Battery
           || roseArm:Arm
           || roseBody:Body
           || roseJoint:Joint
           || roseWifi:WifiInterface
           || roseMotorDriver:MotorDriver
           || roseCameraDriver:CameraDriver
           || roseArmDriver:ArmDriver
           || roseBodyDriver:BodyDriver
)
/* Couple controller-monitor actions to sensor,peripheral,actuator actions */
/{{smAccelerometer,roseAccelerometer}.measure[i:SAFE_RANGE]/controller.safe_acc,
  {smAccelerometer,roseAccelerometer}.measure[j:UNSAFE_RANGE]/controller.unsafe_acc,
  {smCamera,roseCamera}.measure[k:SAFE_RANGE]/controller.safe_detect,
  {smCamera,roseCamera}.measure[l:UNSAFE_RANGE]/controller.unsafe_detect,
  roseLidar.measure[m:SAFE_RANGE]/controller.safe_scn,
  roseLidar.measure[n:UNSAFE_RANGE]/controller.unsafe_scn,
  roseArm.getGripStrengh[o:HIGH_STRENGTH_RANGE]/controller.high_str,
  roseArm.getGripStrengh[o:LOW_STRENGTH_RANGE]/controller.low_str,
  roseBattery.getStatus[q:GOOD_CHARGE_RANGE]/controller.good_bat,
  roseBattery.getStatus[r:LOW_CHARGE_RANGE]/controller.low_bat,
  roseBody.getLift[s:SENSOR_RANGE]/controller.getLift,
  roseJoint.getJointState[t:SENSOR_RANGE]/controller.getJointState,
  roseWifi.getWifiStrength[u:LOW_WIFI_RANGE]/controller.low_wf,
  roseWifi.getWifiStrength[v:GOOD_WIFI_RANGE]/controller.good_wf,
  /* The controller sends a signal to the operator when the robot shows unsafe behaviour. */
  operator.readError/controller.signal
  }.
